import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'idea'
    id 'kr.entree.spigradle' version '2.3.2'
}

group 'com.aeltumn'
version '2.6.2'

sourceCompatibility = 17
compileJava.options.encoding = 'UTF-8'

ext {
    minecraftVersion = '1.18-R0.1-SNAPSHOT'
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url 'https://libraries.minecraft.net/' }
}

dependencies {
    // General Compilation Only Dependencies
    compileOnly "org.spigotmc:spigot-api:${minecraftVersion}"
    compileOnly group: 'com.mojang', name: 'authlib', version: '1.5.21'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.7'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    group 'export'
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    group 'export'
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

task exportAll(dependsOn: [ generateSpigotDescription, build, sourcesJar, javadocJar ]) {
    group 'export'
}

jar {
    manifest {
        attributes(
                'Implementation-Version': version,
                'Implementation-Vendor': "Aeltumn",
                'Built-By': System.properties['user.name'],
                'Build-Timestamp': new SimpleDateFormat("HH:mm dd-MM-yyyy").format(new Date()),
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

spigot {
    description "An open-source autocrafting plugin for Bukkit."
    load STARTUP
    authors "Aeltumn"
    apiVersion '1.18'
    commands {
        reloadrecipes {
            permission "automatedcrafting.reload"
            description "Reloads all recipes for the autocrafters, gets ran automatically when running /reload!"
        }
    }

    permissions {
        'automatedcrafting.reload' {
            defaults "op"
            description "Allows using /reloadrecipes."
        }
        'automatedcrafting.makeautocrafters' {
            defaults "true"
            description "Allows players to make autocrafters."
        }
    }

    debug {
        eula true
        buildVersion '1.18.1'
    }
}