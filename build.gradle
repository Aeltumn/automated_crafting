import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'idea'
}

apply plugin: 'java'

group 'nl.dgoossens'
version '2.3'

sourceCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

ext {
    minecraftVersion = '1.12.2-R0.1-SNAPSHOT'
}

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url 'https://libraries.minecraft.net/' }
}

dependencies {
    // General Compilation Only Dependencies
    compileOnly "org.spigotmc:spigot-api:${minecraftVersion}"
    compileOnly group: 'com.mojang', name: 'authlib', version: '1.5.21'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.7'

    compileOnly files("libs/wolfyutilities-1.6.1.2.jar")
    compileOnly files("libs/customcrafting-1.6.3.0.jar")
}

task generateResources(type: Copy) {
    group 'export'
    from 'src/main/resources'
    into "$buildDir/generated-resources"
    filter { line -> line.replaceAll('%version%', version as String) }
}

task buildJar(type: Jar, dependsOn: generateResources) {
    group 'export'
    from sourceSets.main.output.classesDirs
    from "$buildDir/generated-resources"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    group 'export'
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    group 'export'
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

task exportAll(dependsOn: [ buildJar, sourcesJar, javadocJar ]) {
    group 'export'
}

buildJar {
    manifest {
        attributes(
                'Implementation-Version': version,
                'Implementation-Vendor': "Aeltumn",
                'Built-By': System.properties['user.name'],
                'Build-Timestamp': new SimpleDateFormat("HH:mm dd-MM-yyyy").format(new Date()),
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}